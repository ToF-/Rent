#! /usr/bin/awk -f

# compute maximum profit made out of sorted actions

BEGIN { profit = -1 }

/BEGIN/ { if (profit != -1) {
            print profit
        }
        profit = 0
    }

/CASH/ { cash($2) }

/RENT/ { rent($2, $4, $5) }

END { print profit }

function cash(time) {
    t = key(time)
    profit = max(plan[t], profit)
}

function rent(time,duration,price) {
    cash(time) 
    t = key(time + duration)
    plan[t] = max(profit + price, plan[t]) 
}

function key(t) {
    return(sprintf("%07d",t))
}

function max(a, b) {
    return a > b ? a : b
}

